#!/m1/shared/bin/perl

use strict;
use warnings;

use XML::Simple;
use Getopt::Std;

my $var = '/var/local/vncip';

my %opt;
getopts('tl:', \%opt);

chdir "$var/log" or die "Can't chdir $var/log: $!";

if ($opt{t}) {
    open STDIN, '<', "traindb.log" or die;
}
elsif ($opt{l}) {
    if ($opt{l} ne '-') {
        $opt{l} =~ s/(?:\.log)?$/.log/;
        open STDIN, '<', $opt{l} or die;
    }
}
else {
    my @logs = grep { !/^traindb\.log$/ } <*.log>;
    die "Which log file?" if @logs != 1;
    open STDIN, '<', $logs[0] or die;
}

$/ = '';

my %num;
my %type;
my @match;

foreach (@ARGV) {
    if (m{^/(.+)/$}) {
        push @match, qr/(?m)$1/;
    }
    elsif (/^\d+$/) {
        $num{$_} = 1;
    }
    else {
        $type{$_} = 1;
    }
}

printf "%5s %-1.1s %5.5s %-24.24s %-19.19s %s\n", 'Num', '?', 'Req', 'Message', 'Client', 'Timestamp';
my $n = 0;
while (<STDIN>) {
    $n++;
    next if %num && !$num{$n};
    my ($date, $ipaddr);
    my $xml = '';
    my $response;
    ($_, $response) = split /\n::: WORKING\n/;
    foreach (split /\n/) {
        if (/^(?:\(\(|\)\)) (.+) (\S+)$/) {
            ($date, $ipaddr) = ($1, $2);
        }
        elsif (s/^\|   //) {
            $xml .= $_;
            $xml .= "\n";
        }
    }
    if (length $xml) {
        next if @match && !grep { $xml =~ $_ } @match;
        $xml = XMLin($xml, 'KeepRoot' => 1);
        my $msg = $xml->{'NCIPMessage'} || die "not an NCIP message";
        my $version = delete $msg->{'version'};
        my ($msgtype, $body, @etc) = %$msg;
        die if @etc;
        next if %type && !$type{$msgtype};
        my $reqnum = $body->{'UniqueRequestId'}{'RequestIdentifierValue'} || '';
        my $flag = ($response =~ /<Problem>/ ? 'E' : '');
        printf "%5d %-1.1s %5.5s %-24.24s %-19.19s %s\n", $n, $flag, $reqnum, $msgtype, $ipaddr, $date;
    }
}
